<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="utf-8">
    <meta http-equiv="X-UA-Compatible" content="IE=edge">
    <meta name="viewport" content="width=device-width, initial-scale=1">
    <!-- The above 3 meta tags *must* come first in the head; any other head content must come *after* these tags -->
    <meta name="description" content="">
    <meta name="author" content="">

    <title>Mock3d-Template</title>

    <!-- Google font -->
    <link href='https://fonts.googleapis.com/css?family=Open+Sans:400,300' rel='stylesheet' type='text/css'>

    <!-- Bootstrap core CSS -->
    <link href="css/bootstrap.css" rel="stylesheet"> <!--have bootstrap.min.css-->
    <link href="css/offcanvas.css" rel="stylesheet">
    <link href="css/bootstrap-slider.css" rel="stylesheet">
    <link href="css/jquery.minicolors.css" rel="stylesheet">


    <!-- Custom styles for this template -->
    <link href="css/mock3d.css" rel="stylesheet">

    <script id="vertex-shader" type="x-shader/x-vertex">
        attribute vec3 vPosition;

        // deprecated
        uniform float lightNum;
        uniform vec3 lightColor[1];
        uniform float lightX, lightY, lightZ;
        uniform float Dir_lightX, Dir_lightY, Dir_lightZ;
        uniform float coordX, coordY, coordZ;

        varying vec3 v_position;
        varying vec3 defaultLightColor;

        varying vec3 p_light;
        varying vec3 dir_light;
        varying vec3 p_eye;

        void main() {
            gl_Position = vec4(vPosition, 1.0);
            defaultLightColor = lightColor[0];
            p_light = vec3(lightX, lightY, lightZ);
            dir_light = vec3(Dir_lightX, Dir_lightY, Dir_lightZ);
            p_eye = vec3(coordX, coordY, coordZ);
            v_position = vPosition;
        }
    </script>

    <script id="fragment-shader" type="x-shader/x-fragment">
        #define MAX_TH 1000.0
        #define MIN_TH 0.001
        #define PI 3.14159265359
        #define MAX_ITERATION 10
        #define MAX_SAMPlES 100
        #define REFLECT_LAMBERTIAN 1
        #define REFLECT_SPECULAR 2
        #define REFLECT_DIELECTRIC 3
        #define REFLECT_LIGHT 4
        #define COMMON_SHAPE 0
        #define PLANE_SHAPE 1
        #define SPHERE_SHAPE 2

        precision mediump float;

        varying vec3 v_position;
        varying vec3 defaultLightColor;
        varying vec3 p_light;
        varying vec3 dir_light;
        varying vec3 p_eye;

        uniform sampler2D uRandomTexture;
        uniform sampler2D uSamplerTexture;
        uniform sampler2D defaultTexture;

        uniform int currentLight;

        uniform bool lightsOnly;

        uniform float styleBright, styleDark;

        uniform float specSharpness, specBlurriness;

        uniform float lightIntensity;
        uniform float refractionIndex;
        uniform float lightRadius;
        uniform int tracingSamples;

        float uSamplelight = 0.8;

        struct SurfaceInfo {
            int reflectType;
            int shape;
            vec3 attenuation;
            vec3 emission;
            vec3 center;
        };

        struct HitInfo {
            vec3 pos;
            vec3 normal;
            SurfaceInfo surfaceInfo;
        };

        struct Ray {
            vec3 origin;
            vec3 dir;
        };

        uniform vec2 uResolution;

        int randomindex;
        SurfaceInfo wBottom, hyper, pillar, lambertian, specular, dielectric, light, infinite;

        void initialize() {
            randomindex = int(gl_FragCoord.x * fract(sin(dot(gl_FragCoord.xy * 0.01, vec2(19.342, 54.342)) * 3244.4234)) * 256.0 * 256.0);
            wBottom = SurfaceInfo(REFLECT_LAMBERTIAN, COMMON_SHAPE, vec3(0.0), vec3(0.5), vec3(0.0, -1.0, 0.0));
            hyper = SurfaceInfo(REFLECT_LAMBERTIAN, SPHERE_SHAPE, vec3(0.0), vec3(0.0), vec3(-1.0, 0.0, 0.0));
            pillar = SurfaceInfo(REFLECT_LAMBERTIAN, PLANE_SHAPE, vec3(0.0), vec3(0.0), vec3(-1.0, 1.0, 0.5));
            infinite = SurfaceInfo(REFLECT_LAMBERTIAN, PLANE_SHAPE, vec3(0.0), vec3(0.0), vec3(0.0, 0.0, 0.0));

            lambertian = SurfaceInfo(REFLECT_LAMBERTIAN, SPHERE_SHAPE, vec3(0.0), vec3(0.0), vec3(0.2, -0.6, -0.5));
            specular = SurfaceInfo(REFLECT_SPECULAR, SPHERE_SHAPE, vec3(0.0), vec3(0.0), vec3(-0.5, -0.2, -1.0));
            dielectric = SurfaceInfo(REFLECT_DIELECTRIC, SPHERE_SHAPE, vec3(0.0), vec3(0.0), vec3(0.5, -0.6, 0.5));
            light = SurfaceInfo(REFLECT_LIGHT, COMMON_SHAPE, vec3(0.0), defaultLightColor * lightIntensity * 50.0, p_light);
        }

        vec3 at(in Ray ray, in float t) {
            return ray.origin + t * ray.dir;
        }

        bool rectXY(in vec3 pc, in float radius, in float tmin, inout float tmax, in Ray ray, in SurfaceInfo surfaceInfo, inout HitInfo hitInfo) {
            float x1 = pc[0] - radius;
            float x2 = pc[0] + radius;
            float y1 = pc[1] - radius;
            float y2 = pc[1] + radius;
            float z = pc[2];

            float t = (z - ray.origin.z) / ray.dir.z;
            vec3 p = at(ray, t);
            if (t < tmin || t > tmax || p.x < x1 || p.x > x2 || p.y < y1 || p.y > y2) return false;
            tmax = t;
            hitInfo.pos = p;
            hitInfo.normal = vec3(0.0, 0.0, 1.0);
            hitInfo.surfaceInfo = surfaceInfo;
            return true;
        }

        bool rectYZ(in vec3 pc, in float radius, in float tmin, inout float tmax, in Ray ray, in SurfaceInfo surfaceInfo, inout HitInfo hitInfo) {
            float y1 = pc[1] - radius;
            float y2 = pc[1] + radius;
            float z1 = pc[2] - radius;
            float z2 = pc[2] + radius;
            float x = pc[0];

            float t = (x - ray.origin.x) / ray.dir.x;
            vec3 p = at(ray, t);
            if (t < tmin || t > tmax || p.y < y1 || p.y > y2 || p.z < z1 || p.z > z2) return false;
            tmax = t;
            hitInfo.pos = p;
            hitInfo.normal = vec3(1.0, 0.0, 0.0);
            hitInfo.surfaceInfo = surfaceInfo;
            return true;
        }

        bool rectZX(in vec3 pc, in float radius, in float tmin, inout float tmax, in Ray ray, SurfaceInfo surfaceInfo, inout HitInfo hitInfo) {
            float z1 = pc[2] - radius;
            float z2 = pc[2] + radius;
            float x1 = pc[0] - radius;
            float x2 = pc[0] + radius;
            float y = pc[1];

            float t = (y - ray.origin.y) / ray.dir.y;
            vec3 p = at(ray, t);
            if (t < tmin || t > tmax || p.z < z1 || p.z > z2 || p.x < x1 || p.x > x2) return false;
            tmax = t;
            hitInfo.pos = p;
            hitInfo.normal = vec3(0.0, 1.0, 0.0);
            hitInfo.surfaceInfo = surfaceInfo;
            return true;
        }

        bool general_quadrics(vec3 pc,
            in float a02, in float a12, in float a22, in float a21, in float a00, in float s0, in float s1, in float s2,
            in vec3 v2, in vec3 vup, in float tmin, inout float tmax,
            in Ray ray, in SurfaceInfo surfaceInfo, inout HitInfo info) {

            vec3 pe = ray.origin;
            vec3 npe = ray.dir;

            // find roots
            vec3 n0 = normalize(cross(vup, v2));
            vec3 n1 = normalize(cross(n0, v2));
            vec3 n2 = normalize(v2);

            float A = a02 * dot(n0, npe) * dot(n0, npe) / (s0 * s0) + a12 * dot(n1, npe) * dot(n1, npe) / (s1 * s1) +
            a22 * dot(n2, npe) * dot(n2, npe) / (s2 * s2);
            float B = 2.0 * a02 * dot(n0, npe) * dot(n0, pe - pc) / (s0 * s0) +
            2.0 * a12 * dot(n1, npe) * dot(n1, pe - pc) / (s1 * s1) +
            2.0 * a22 * dot(n2, npe) * dot(n2, pe - pc) / (s2 * s2) + a21 * dot(n2, npe) / s2;
            float C = a02 * dot(n0, pe - pc) * dot(n0, pe - pc) / (s0 * s0) +
            a12 * dot(n1, pe - pc) * dot(n1, pe - pc) / (s1 * s1) +
            a22 * dot(n2, pe - pc) * dot(n2, pe - pc) / (s2 * s2) + a21 * dot(n2, pe - pc) / s2 + a00;


            float delta = B * B - 4.0 * A * C;
            if (a21 > 0.0) {
                float t = -C / B;
                if (t > tmin && t < tmax) {
                    tmax = t;
                    info.pos = at(ray, t);
                    info.normal = normalize(info.pos - pc);
                    info.surfaceInfo = surfaceInfo;
                    return true;
                }
            } else {
                if (delta < 0.0) return false;
                float t = (-B - sqrt(delta)) / (2.0 * A);
                if (t > tmin && t < tmax) {
                    tmax = t;
                    info.pos = at(ray, t);
                    info.normal = normalize(info.pos - pc);
                    info.surfaceInfo = surfaceInfo;
                    return true;
                }
                t = (-B + sqrt(delta)) / (2.0 * A);
                if (t > tmin && t < tmax) {
                    tmax = t;
                    info.pos = at(ray, t);
                    info.normal = normalize(info.pos - pc);
                    info.surfaceInfo = surfaceInfo;
                    return true;
                }
            }
            return false;
        }

        bool general_quadrics(
            in float a02, in float a12, in float a22, in float a21, in float a00, in float s0, in float s1, in float s2,
            in vec3 v2, in vec3 vup, in float tmin, inout float tmax,
            in Ray ray, in SurfaceInfo surfaceInfo, inout HitInfo info) {
            return general_quadrics(surfaceInfo.center, a02, a12, a22, a21, a00, s0, s1, s2, v2, vup, tmin, tmax, ray, surfaceInfo, info);
        }

        bool intersect(in Ray ray, in float tmin, in float tmax, inout HitInfo info) {
            bool hit = false;
            hit = rectZX(vec3(0.0, -1.0, 0.0), 1000.0, tmin, tmax, ray, wBottom, info) || hit;
            hit = general_quadrics(p_light, 1., 1., 1., 0., -1., lightRadius, lightRadius, lightRadius, vec3(0., 0., -1.), vec3(0., 1., 0.),
                tmin, tmax, ray, light, info) || hit;

            hit = general_quadrics(1., -1., 1., 0., -1., 0.01, 0.03, 0.01, vec3(0., 0., -1.), vec3(0., 1., 0.),
                tmin, tmax, ray, hyper, info) || hit;
            hit = general_quadrics(1., 0., 1., 0., -1., 0.03, 0.03, 0.03, vec3(0., 0., -1.), vec3(0., 1., 0.),
            tmin, tmax, ray, pillar, info) || hit;


            hit = general_quadrics(1., 1., 1., 0., -1., 0.4, 0.4, 0.4, vec3(0., 0., -1.), vec3(0., 1., 0.),
            tmin, tmax, ray, dielectric, info) || hit;
            hit = general_quadrics(1., 1., 1., 0., -1., 0.4, 0.4, 0.4, vec3(0., 0., -1.), vec3(0., 1., 0.),
            tmin, tmax, ray, specular, info) || hit;
            hit = general_quadrics(1., 1., 1., 0., -1., 0.4, 0.4, 0.4, vec3(0., 0., -1.), vec3(0., 1., 0.),
            tmin, tmax, ray, lambertian, info) || hit;
            hit = general_quadrics(1., 1., 1., 0., -1., 5.0, 5.0, 5.0, vec3(0., 0., -1.), vec3(0., 1., 0.),
            tmin, tmax, ray, infinite, info) || hit;
            return hit;
        }


        vec2 randompos() {
            int h = randomindex / 256;
            int w = randomindex  - h * 256;
            return vec2(float(w) / 256.0, float(h) / 256.0);
        }

        vec3 random3() {
            randomindex += 1;
            return texture2D(uRandomTexture, randompos()).xyz;
        }

        vec2 random2() {
            return random3().xy;
        }

        float random() {
            return random3().x;
        }

        vec3 randomCosineHemisphere() {
            float r1 = random();
            float r2 = random();
            float z = sqrt(1.0 - r2);
            float phi = 2.0 * PI * r1;
            float x = cos(phi) * sqrt(r2);
            float y = sin(phi) * sqrt(r2);
            return vec3(x, y, z);
        }

        mat3 orthonormal(in vec3 z) {
            vec3 w = normalize(z);
            vec3 u = normalize(cross(w, abs(w.x) > 0.9 ? vec3(0.0, 1.0, 0.0) : vec3(1.0, 0.0, 0.0)));
            vec3 v = normalize(cross(u, w));
            return mat3(u, v, w);
        }

        float cosinePdfValue(in HitInfo info, in vec3 dir) {
            float c = dot(normalize(dir), info.normal);
            return c > 0.0 ? c / PI : 0.0;
        }

        vec3 cosinePdfDir(in HitInfo info) {
            return -orthonormal(info.normal) * randomCosineHemisphere();
        }

        float lightPdfValue(in HitInfo info, in vec3 dir) {
            Ray ray = Ray(info.pos, dir);
            HitInfo hi;
            SurfaceInfo si;
            float t = MAX_TH;
            // here hi and si are only for occupying the function position, so we need to pass the real center of light
            if (general_quadrics(p_light, 1., 1., 1., 0., -1., lightRadius, lightRadius, lightRadius, vec3(0., 0., -1.), vec3(0., 1., 0.),
                MIN_TH, t, ray, si, hi)) {
                float center_d = length(info.pos - p_light);
                float area = 2.0 * PI * lightRadius * lightRadius * (center_d - lightRadius) / center_d;
                float d2 = pow(length(dir) * 2.0, 2.0);
                float cosine = abs(dot(dir, hi.normal)) / length(dir);
                return d2 / (cosine * area);
            }
            return 0.0;
        }

        vec3 lightPdfDir(in HitInfo info) {
            // sphere
            vec3 onLight = vec3(p_light[0] - lightRadius + 2.0 * lightRadius * random(),
                p_light[1] - lightRadius + 2.0 * lightRadius * random(),
                p_light[2] - lightRadius + 2.0 * lightRadius * random());
            vec3 toLight = onLight - info.pos;
            float dist = dot(toLight, toLight);
            toLight = normalize(toLight);
            vec3 dir = toLight;
            return dir;
        }

        float mixPdfValue(in HitInfo info, in vec3 dir) {
            return (1.0 - uSamplelight) * cosinePdfValue(info, dir) + uSamplelight * lightPdfValue(info, dir);
        }

        vec3 mixPdfDir(in HitInfo info) {
            return random() < uSamplelight ? lightPdfDir(info) : cosinePdfDir(info);
        }

        float schlick(float cosine, float ref) {
            float r0 = (1.0 - ref) / (1.0 + ref);
            r0 = r0 * r0;
            return r0 + (1.0 - r0) * pow((1.0 - cosine), 5.0);
        }

        vec3 trace(in Ray ray) {
            vec3 c = vec3(1.0);
            for (int i = 0; i < MAX_ITERATION; i++) {
                HitInfo info;
                if (intersect(ray, MIN_TH, MAX_TH, info)) {
                    if (info.surfaceInfo.reflectType == REFLECT_LAMBERTIAN) {
                        vec3 normal = dot(ray.dir, info.normal) < 0.0 ? info.normal : -info.normal;
                        info.normal = normal;
                        vec3 dir = mixPdfDir(info);
                        float p = mixPdfValue(info, dir);
                        if (p <= 0.0) return vec3(0.0);

                        vec4 texture;
                        if (info.surfaceInfo.shape == PLANE_SHAPE) {
                            float x = dot(vec3(1., 0., 0.), info.pos - info.surfaceInfo.center);
                            float y = dot(vec3(0., 1., 0.), info.pos - info.surfaceInfo.center);
                            float u = x - float(floor(x));
                            if (x < 0.0)
                            u = 1.0 - u;
                            float v = y - float(floor(y));
                            if (y < 0.0)
                            v = 1.0 - v;
                            texture = texture2D(defaultTexture, vec2(u, v));
                        } else if (info.surfaceInfo.shape == SPHERE_SHAPE){
                            float x = dot(vec3(1., 0., 0.), info.pos - info.surfaceInfo.center);
                            float y = dot(vec3(0., 1., 0.), info.pos - info.surfaceInfo.center);
                            float z = dot(vec3(0., 0., 1.), info.pos - info.surfaceInfo.center);
                            float v = acos(z) / PI;
                            float u = acos(y / sin(PI * v)) / (2.0 * PI);
                            texture = texture2D(uSamplerTexture, vec2(u, v));
                        } else {
                            texture = vec4(info.surfaceInfo.emission, 1);
                        }

                        c *= p > 0.0 ? vec3(texture[0], texture[1], texture[2]) * dot(info.normal, dir) / (PI * p) : vec3(0.0);
                        ray = Ray(info.pos, dir);
                    } else if (info.surfaceInfo.reflectType == REFLECT_SPECULAR) {
                        vec3 normal = dot(ray.dir, info.normal) < 0.0 ? info.normal : -info.normal;
                        ray = Ray(info.pos, reflect(ray.dir, normal));
                    } else if(info.surfaceInfo.reflectType == REFLECT_DIELECTRIC) {
                        vec3 n;
                        float eta, cosine;
                        if (dot(ray.dir, info.normal) < 0.0) {
                            n = info.normal;
                            eta = 1.0 / refractionIndex;
                            cosine = -dot(ray.dir, info.normal) / length(ray.dir);
                        } else {
                            n = -info.normal;
                            eta = refractionIndex;
                            cosine = refractionIndex * dot(ray.dir, info.normal) / length(ray.dir);
                        }
                        vec3 r = refract(normalize(ray.dir), n, eta);
                        vec3 dir = r == vec3(0.0) || random() < schlick(cosine, refractionIndex) ? reflect(ray.dir, n) : r;
                        ray = Ray(info.pos, dir);
                    } else if (info.surfaceInfo.reflectType == REFLECT_LIGHT) {
                        // less than 0.0 with sphere, larger than 0.0 with rect plane
                        c *= dot(info.normal, ray.dir) < 0.0 ? info.surfaceInfo.emission : vec3(0.0);
                        return c;
                    }
                } else {
                    return vec3(0.0);
                }
            }
            return vec3(0.0);
        }

        mat3 camera(in vec3 pos, in vec3 tar, in vec3 up) {
            vec3 cz = normalize(tar - pos);
            vec3 cx = normalize(cross(cz, normalize(up)));
            vec3 cy = normalize(cross(cx, cz));
            return mat3(cx, cy, cz);
        }

        Ray ray(in mat3 cam, in vec2 uv, in vec3 origin) {
            vec3 dir = cam * normalize(vec3(uv, 1.0));
            Ray ray = Ray(origin, dir);
            return ray;
        }

        void main() {
            initialize();
            vec3 tar = vec3(0.0, 0.0, 0.0);
            mat3 cam = camera(p_eye, tar, vec3(0.0, 1.0, 0.0));
            vec3 c = vec3(0.0);
            for (int i = 1; i <= MAX_SAMPlES; i++) {
                vec2 uv = gl_FragCoord.xy + random2();
                vec2 st = (uv * 2.0 - uResolution) / uResolution.y;
                Ray ray = ray(cam, st, p_eye);
                c += trace(ray);
                if (tracingSamples == i) break;
            }
            c /= float(tracingSamples < MAX_SAMPlES ? tracingSamples : MAX_SAMPlES);
            c = pow(c, vec3(1.0 / 2.2));
            gl_FragColor = vec4(c, 1.0);
        }
    </script>

    <script type="text/javascript" src="lib/jquery-2.1.4.js"></script>
    <script type="text/javascript" src="scripts/webgl-utils.js"></script>
    <script type="text/javascript" src="scripts/initShaders.js"></script>
    <script type="text/javascript" src="scripts/MV.js"></script>


</head>

<body>
<!--nav-->
<nav class="navbar navbar-inverse navbar-fixed-top" role="navigation">
    <ul class="fancyNav">
        <li><a class="active" href=index.html>3D Objects</a></li>
    </ul>
</nav>

<!--content-->
<div class="container-fluid">
    <div class="row row-offcanvas row-offcanvas-left">
        <!--Left Column start-->
        <div class="col-sm-2 sidebar sidebar-left sidebar-offcanvas">

            <ul class="myAccordion" id="accordion_L">
                <!--Basic Images-->
                <li class="panel">
                    <a class="myControlTitle" data-toggle="collapse" data-parent="#accordion_L" href="#basicImages">
                        Texture Images
                    </a>
                    <div id="basicImages" class="inCollapse collapse in">

                        <div class="imgbox_container">
                            <h6><span title="Insert or DragnDrop texture image here">Solid Texture Image</span></h6>
                            <div>
                                <div id="container1" class="imgThumb_container">
                                    <div class="btn-file">
                                        <input type="file" id="inputFileToLoad1">
                                    </div>
                                    <div id="container1image" class="imgThumb"></div>
                                </div>
                            </div>
                        </div>

                        <div class="imgbox_container">
                            <h6><span title="Insert or DragnDrop texture image here">Texture Mapping Image</span></h6>
                            <div>
                                <div id="container2" class="imgThumb_container">
                                    <div class="btn-file">
                                        <input type="file" id="inputFileToLoad2">
                                    </div>
                                    <div id="container2image" class="imgThumb"></div>
                                </div>
                            </div>
                        </div>
                    </div>
                </li>

            </ul>
        </div>
        <!--Left Column end-->
        <!--Main Canvas start-->
        <div class="col-sm-8 main">
            <!--
            <p class="pull-left visible-xs">
               <button type="button" class="btn btn-primary btn-xs" data-toggle="offcanvas">Toggle nav</button>
            </p>
            -->
            <!--
           <img src="img/solidColor.jpg" class="img-responsive" alt="our canvas">
           -->
            <div class="canvas_container">
                <canvas id="gl-canvas">
                    Oops ... your browser doesn't support the HTML5 canvas element
                </canvas>

                <svg id="lightPosition_container" xmlns="http://www.w3.org/2000/svg">


                </svg>

            </div>

        </div>
        <!--Main Canvas end-->

        <!--Right column start-->
        <div class="col-sm-2 sidebar sidebar-right">
            <ul class="myAccordion" id="accordion_R">
                <!--Light-->
                <li class="panel">
                    <a class="myControlTitle collapsed" data-toggle="collapse" data-parent="#accordion_R"
                       href="#lightControl">
                        Light
                    </a>
                    <div id="lightControl" class="inCollapse collapse">

                        <div class="slider_container">
                            <span class="h6 inlineBlock"> Path Tracing Samples </span>
                            <div class="pull-right">
                                <input type="text" class="form-control mytextbox text-center" id="tracingSamples">
                            </div>
                            <div class="slider_box">
                                <input id="tracingSamples_silder" data-slider-id='customSlider' type="text"/>
                            </div>
                        </div>

                        <div class="slider_container">
                            <span class="h6 inlineBlock"> Refraction Index </span>
                            <div class="pull-right">
                                <input type="text" class="form-control mytextbox text-center" id="refractionIndex">
                            </div>
                            <div class="slider_box">
                                <input id="refractionIndex_silder" data-slider-id='customSlider' type="text"/>
                            </div>
                        </div>


                        <div class="slider_container">
                            <span class="h6 inlineBlock"> Light Intensity </span>
                            <div class="pull-right">
                                <input type="text" class="form-control mytextbox text-center" id="lightIntensity">
                            </div>
                            <div class="slider_box">
                                <input id="lightIntensity_silder" data-slider-id='customSlider' type="text"/>
                            </div>
                        </div>

                        <div class="slider_container">
                            <span class="h6 inlineBlock"> Light Radius </span>
                            <div class="pull-right">
                                <input type="text" class="form-control mytextbox text-center" id="lightRadius">
                            </div>
                            <div class="slider_box">
                                <input id="lightRadius_silder" data-slider-id='customSlider' type="text"/>
                            </div>
                        </div>

                        <div id="lights_container">
                            <ul class="myAccordion" id="accordion_Lights">

                            </ul>
                        </div>
                    </div>
                </li>

                <li class="panel">
                    <a class="myControlTitle collapsed" data-toggle="collapse" data-parent="#accordion_R"
                       href="#coordinate">
                        Light Position
                    </a>
                    <div id="coordinate" class="inCollapse collapse">

                        <div class="slider_container">
                            <span class="h6 inlineBlock"> X </span>
                            <div class="pull-right">
                                <input type="text" class="form-control mytextbox text-center" id="light_X">
                            </div>
                            <div class="slider_box">
                                <input id="X_slider" data-slider-id='customSlider' type="text"/>
                            </div>
                        </div>
                        <div class="slider_container">
                            <span class="h6 inlineBlock"> Y </span>
                            <div class="pull-right">
                                <input type="text" class="form-control mytextbox text-center" id="light_Y">
                            </div>
                            <div class="slider_box">
                                <input id="Y_slider" data-slider-id='customSlider' type="text"/>
                            </div>
                        </div>
                        <div class="slider_container">
                            <span class="h6 inlineBlock"> Z </span>
                            <div class="pull-right">
                                <input type="text" class="form-control mytextbox text-center" id="light_Z">
                            </div>
                            <div class="slider_box">
                                <input id="Z_slider" data-slider-id='customSlider' type="text"/>
                            </div>
                        </div>

                    </div>
                </li>
            </ul>
        </div>
        <!--Right column end-->
    </div>
</div>


<!-- light templete -->
<script type="text/template" data-template="light">
    <li class="panel" data-id="{{id}}" id="lightPanel{{id}}">
        <a class="myControlTitle myLightsTitle collapsed" data-toggle="collapse" data-parent="#accordion_Lights"
           href="#light{{id}}">
            <div class="destroy"></div>
            LIGHT{{id}}
        </a>
        <div id="light{{id}}" class="lightBox inCollapse collapse">
            <div>
                <span class="h6 inlineBlock"> Color </span>
                <div class="pull-right">
                    <input class="colorPicker" type="hidden">
                </div>
            </div>
        </div>
    </li>

</script>

<!-- Bootstrap core JavaScript  ================================================== -->
<script src="lib/bootstrap.min.js"></script>
<script src="lib/bootstrap-slider.js"></script>

<!--colorpicker-->
<script src="lib/jquery.minicolors.min.js"></script>

<!-- IE10 viewport hack for Surface/desktop Windows 8 bug -->
<script src="lib/ie10-viewport-bug-workaround.js"></script>

<!-- More -->
<script type="text/javascript" src="scripts/dragndrop.js"></script>
<script type="text/javascript" src="scripts/cube.js"></script>
<script type="text/javascript" src="scripts/sliders.js"></script>
<script type="text/javascript" src="scripts/addLight.js"></script>

</body>
</html>
